Import("env")

module_env = env.Clone()

if not env.msvc:
    module_env.Append(CCFLAGS=["-Wno-error=non-virtual-dtor"])
    module_env.Append(CCFLAGS=["-Wno-error=ctor-dtor-privacy"])

module_env.Append(
    CPPDEFINES=[
        "HAVE_CONFIG_H",
        "PACKAGE=",
        "VERSION=",
        "CPU_CLIPS_POSITIVE=0",
        "CPU_CLIPS_NEGATIVE=0",
        "WEBRTC_APM_DEBUG_DUMP=0",
        "WHISPER_BUILD",
    ]
)

enable_webrtc_logging = env["target"] == "debug"

if not enable_webrtc_logging:
    module_env.Append(CPPDEFINES=["RTC_DISABLE_LOGGING", "RTC_DISABLE_METRICS"])

if env["platform"] == "windows" or env["platform"] == "uwp":
    module_env.Append(CPPDEFINES=["WEBRTC_WIN"])
elif env["platform"] == "ios":
    module_env.Append(CPPDEFINES=["WEBRTC_POSIX", "WEBRTC_IOS"])
elif env["platform"] == "macos":
    module_env.Append(CPPDEFINES=["WEBRTC_POSIX", "WEBRTC_MAC"])
elif env["platform"] == "linuxbsd":
    module_env.Append(CPPDEFINES=["WEBRTC_POSIX", "WEBRTC_LINUX"])
elif env["platform"] == "android":
    module_env.Append(CPPDEFINES=["WEBRTC_POSIX", "WEBRTC_ANDROID"])
else:  # including if env["platform"] == "javascript":
    module_env.Append(CPPDEFINES=["WEBRTC_POSIX"])

module_env.Prepend(CPPPATH=["thirdparty/libsamplerate/src"])
module_env.Prepend(CPPPATH=["include"])

env_thirdparty = module_env.Clone()
env_thirdparty.disable_warnings()

env_thirdparty.Prepend(CPPPATH=["#thirdparty/libogg"])

env_thirdparty.add_source_files(env.modules_sources, Glob("thirdparty/libsamplerate/src/*.c"))
env_thirdparty.add_source_files(env.modules_sources, Glob("thirdparty/whisper.cpp/*.c"))
env_thirdparty.add_source_files(env.modules_sources, Glob("thirdparty/whisper.cpp/whisper.cpp"))
env_thirdparty.Append(CPPPATH=['thirdparty/whisper.cpp'])
env_thirdparty.Append(CPPDEFINES=['WHISPER_SHARED', 'GGML_SHARED'])
module_env.add_source_files(env.modules_sources, "*.cpp")
